[style name="narration"]
[music path="res://sounds/ost1.wav" fade="0.0" volume="0.0"]
[background arg="res://bgs/dacity.png" fade="0.0"]
we open on da city
so great to be here
\if ur lucky ;)
if {Lifetime.NumRuns} == 0.0:
	wait are you lucky?
	[clear time="1.0" portraits="false" background="false" positions="false" style="false"]
	[style name="monty_chat_questions"]
	- yeah
		[style name="narration"]
		nahhhhhh
		set {CurrentRun.Luck} = -1.0
	- a little *smirk*
		set {CurrentRun.Luck} = 1.0
		[style name="narration"]
		ok sure bud why not
	- middle choice
	- luck doesnt exist
		[style name="narration"]
		lucky you this isnt a vindictive game
		set {CurrentRun.Luck} = 1.0
	- no
		set {CurrentRun.Luck} = -1.0
		[style name="narration"]
		yeah you aint lucky
	[music path="res://sounds/ost1.wav" fade="0.0" volume="0.0"]
	[style name="narration"]
if {CurrentRun.Luck} == 0.0:
	you btw, are not lucky
	only lucky in that you have a dog
elif {CurrentRun.Luck} < 1.0:
	'noticeably unlucky' is the name of your caste
	not one of the good ones, sadly
	well maybe things can improve?
	heres a dog anyway
elif {CurrentRun.Luck} <= 2.0:
	you could be luckier
	but who's compliainin
	not you
	even got a dog
elif {CurrentRun.Luck} <= 100.0:
	for some reason you are luckier than most
	seems unfair [pause=1.0] probably is!
	where is ur dog btw?
[sound path="res://sounds/sqee.wav" volume="0.0"]
oh here he come
join monty right
[style name="monty_chat_questions"]
monty: hi its me the dog
monty: i........
monty: i love you . . .[pause=0.5]<3
leave --All--
[clear time="1.0" music="false"]
[background arg="res://bgs/monty next window.png" fade="0.0"]
[style name="monty_chat_questions"]
label you love the city?
monty: i love the city
monty: <even though it dont love me back/*sniff* its got that stank that break the bank/kinda place that makes you miss dyin>
monty: heh, what about you bub?
[clear time="1.0" portraits="false" music="false" background="false" positions="false" style="false"]
- tha city can blow me | [if {CurrentRun.Luck} == 0.0]
	monty: aint that the truth
	if {CurrentRun.Luck} < 1.0:
		monty: i will not blow you tho
		monty: i am a dog
		monty: well,[pause=0.5] so [pause=0.25]
- im unlucky & [br]blaming others | [if {CurrentRun.Luck} < 0.0]
	monty: haha lol sucks to suck
- big up da city mon | [if {CurrentRun.Luck} > 0.0]
	monty: why are you saying that to a dog
- you are a dog
	monty: \:) yeah [pause=0.5] i guess i am
	[style name="narration"]
	[sound path="res://sounds/lucky.wav" volume="0.0"]
	luck increase by 10! <that seems like a lot!/really???/why not?>
	set {CurrentRun.Luck} += 10.0
	[style name="monty_chat_questions"]
monty: anyway how we doin?
[clear time="1.0" portraits="false" music="false" background="false" positions="false" style="false"]
- gotta poop/piss
- gotta piss and shit
- really gotta shit
- mostly full of piss
monty: <oh of course}/most definitely/that is normal>
label go to the toilet
monty: you should go to the toilet
[clear time="1.0" portraits="false" music="false" background="false" positions="false" style="false"]
- yeah
	monty: yay \:)
- no
	monty: shut up and go >\:(
	[style name="narration"]
	[sound path="res://sounds/unlucky.wav" volume="0.0"]
	luck decrease by 1!
	set {CurrentRun.Luck} += -1.0
	[style name="monty_chat_questions"]
if {CurrentRun.CanGoOutside} == true:
	[style name="narration"]
	right, the toilet, the great outdoors
	love to piss and shit on sidewalks
	if {CurrentRun.Luck} > 1.0:
		and parked cars
	[clear time="1.0"]
	jump go_outside/
[clear time="1.0"]
jump go_to_toilet/
